// Sample build.gradle used in making ADT/AndMore projects work with Android Studio
// while making only minimal or no changes to the filesystem hierarchy.
// See https://androidcookbook.com/r/5203 for caveats and details!
// See https://developer.android.com/studio/ for more on Android Studio.
// See https://gradle.org/ for more on Gradle

apply plugin: 'com.android.application'

buildscript {
	repositories {
		jcenter()
		maven {
			url 'https://maven.google.com/'
			name 'Google'
		}
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:4.0.0'
	}
}

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"

	defaultConfig {
		// applicationId "YOUR.PACKAGE.NAME.HERE"
		minSdkVersion 14
		targetSdkVersion 29
		versionCode 1
		versionName "1.0"
	}

	sourceSets {
		main {
			// Comment OUT following line if your project follows Maven/gradle structure src/main/java
			java.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
			manifest.srcFile 'AndroidManifest.xml'
		}

		androidTest.setRoot('tests')
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation 'androidx.appcompat:appcompat:1.1.0'
}

repositories {
	jcenter()
	maven {
		url 'https://maven.google.com/'
		name 'Google'
	}
}
